apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

archivesBaseName = 'jhaml'
group = 'com.cadrlife.jhaml'


gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(release)) {
        version = '0.1.4'
    } else {
        version = '0.1.4-SNAPSHOT'
    }
}
sourceCompatibility = 1.5

repositories {
    mavenCentral()
    // for markdownj
    mavenRepo(url : "http://scala-tools.org/repo-releases")
}

dependencies {
    compile group: 'commons-lang', name: 'commons-lang', version: '2.5'
    compile group: 'com.google.guava', name: 'guava', version: 'r06'
    compile group: 'org.markdownj', name: 'markdownj', version: '0.3.0-1.0.2b4'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task copyToLib(dependsOn: build, type: Copy) {
    into "lib"
    from configurations.runtime
}

uploadArchives {
    repositories {
        flatDir(dirs: file('dist'))
        mavenDeployer {
            repository(url: "file:///home/ray/repos/raymyers-mvn-repo/releases")
            snapshotRepository(url: "file:///home/ray/repos/raymyers-mvn-repo/snapshots")
        
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task dist(dependsOn: [copyToLib, uploadArchives])

task release(dependsOn: [dist])
